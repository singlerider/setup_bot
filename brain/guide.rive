! version = 2.00

// This file tests topic inclusions and inheritence:
//
// includes: this means that the topic "includes" the triggers present
//           in another topic. Matching triggers in the source and included
//           topic are possible, and the *reply* in the source topic overrides
//           the reply in the included topic.
// inherits: all triggers in the source topic have higher matching priority than
//           all triggers in the inherited topic. So if the source topic has a
//           trigger of simply *, it means NO triggers can possibly match on the
//           inherited topic, because '*' goes higher in the match list.

> object get_name python
    username = rs.current_user()
    return username
< object

// This gets us into the process.
+ *
- Whoa! You've found me! My sole reason for existing is helping you out. {topic=lobby}{@lobby}

// Global triggers available on Earth
> topic lobby

	+ lobby
	- Welcome to the lobby <call>get_name</call>!\s
	^ Type "help" to learn what I can do for you.

	+ help
	- Commands that might be helpful:\s
	^ "first time" - I'll walk you through setup, no prob.\s
	^ "question" - Got a question? Let's see if I can help.\s

	+ [*] first time [*]
	- {topic=lobby}{@help}

	+ [*] question [*]
	- {topic=question}{@help}

< topic

> topic question

	+ help
	- What's up? HeyGuys
	- How can I help?
	- Ask away!
	- Tell me what you need!

	+ [*] (alert box|alert|box) [*]
	- {topic=alert_box}{@help}

	+ (home|lobby)
	- {topic=lobby}{@lobby}

< topic

> topic alert_box

	+ help
	- You can access your dashboard at http://www.twitchalerts.com/dashboard/alert-box-v3What\s
	^ would you like to know about your Alert Box?

	+ [*] (obs|open broadcast software|open broadcaster software) [classic]
	- Check out our tutorial for OBS Classic at https://www.youtube.com/watch?v=JvX5rpEMeew

	+ [*] (obs|open broadcast software|open broadcaster software) studio
	- Check out our tutorial for OBS Studio at https://www.youtube.com/watch?v=yffHVAPHR2w

	+ [*] xsplit [*]
	- Checkout our tutorial for XSplit at https://www.youtube.com/watch?v=w058zjHfAW0

	+ [*] test [*]
	- You can send test alerts for follows, subscriptions, donations, and hosts\s
	^ from your dashboard -\s
	^ Your widget must be open for this to work (click "Launch" on the dashboard).\s
	^ Type in your streaming software (OBS Classic/Studio, Xsplit) if you'd like more information:\s

	+ [*] (media|share|media share|video|sharing|media sharing) [*]
	- For a full explanation of media sharing, check out https://www.youtube.com/watch?v=aJKzVXnY5DU -\s
	^ You can set a minimum donation amount, volume, price per second, and max\s
	^ duration of the media being played. Don't worry, you can always skip a clip\s
	^ if you'd like!

	+ (home|lobby)
	- {topic=lobby}{@lobby}

< topic
