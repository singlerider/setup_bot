! version = 2.00

// This file tests topic inclusions and inheritence:
//
// includes: this means that the topic "includes" the triggers present
//           in another topic. Matching triggers in the source and included
//           topic are possible, and the *reply* in the source topic overrides
//           the reply in the included topic.
// inherits: all triggers in the source topic have higher matching priority than
//           all triggers in the inherited topic. So if the source topic has a
//           trigger of simply *, it means NO triggers can possibly match on the
//           inherited topic, because '*' goes higher in the match list.


> object get_name python
    username = rs.current_user()
    return username
< object

// This gets us into the process.
+ *
- Whoa! You've found me! My sole reason for existing is helping you out.\s
^ Never give your TwitchAlerts or Twitch password to anyone. {topic=lobby}{@lobby}

> topic question

	+ help
	- What's up? HeyGuys
	- How can I help?
	- Ask away!
	- Tell me what you need!

	//acount
	+ [*] dashboard [*]
	- {topic=dashboard}{@help}

	+ [*] (setting|settings|currency|type|min|max|minimum|maximum) [*]
	- {topic=donation_settings}{@help}

	+ [*] (paypal|link|account) [*]
	- {topic=donation_settings}{@paypal}

	+ [*] (donation|donations|donor|donors|tip|tips) [*]
	- {topic=my_donations}{@help}

	+ [*] (subscribers|subs|subscriber|sub) [*]
	- {topic=my_subscribers}{@help}

	+ [*] goal [*]
	- {topic=donation_goal_settings}{@help}

	// widgets
	+ [*] (alert|box|alerts) [*]
	- {topic=alert_box}{@help}

	+ [*] (stream|streams|label|labels) [*]
	- {topic=stream_labels}{@help}

	+ (home|lobby)
	- {topic=lobby}{@lobby}

  + [*] (fee|charge|charges) [*]
  - We don't take a fee from any of your donations and never will! PayPal and our credit card\s
  ^ vendor(s) have charges for their services (https://www.paypal.com/webapps/mpp/paypal-fees)

  + id *
  - {topic=<star>}{@help}

< topic

> topic lobby inherits question

	+ lobby
	- Welcome to the lobby <call>get_name</call>!\s
	^ Type "help" to learn what I can do for you.

	+ help
	- Commands that might be helpful:\s
	^ "question" - Got a question? Let's see if I can help.\s
  ^ "first time setup" - I'll walk you through everything you need!

	+ [*] (question|axe|ask|wonder|wondering|frustrate|frustrated|frustrating) [*]
	- {topic=question}{@help}

  + [*] (first|time|setup) [*]
  - {topic=first_time}{@help}

< topic
